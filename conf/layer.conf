# We have a conf and classes directory, add to BBPATH
# Note how we PREpend BBPATH. This is due to the fact that
# bitbake does not override classes per layer as it does
# recipes. Classes are processed strictly in BBPATH order.
# As we unfortunately have to override standard classes for
# bug fixing, we prepend instead of append our path.

#This layer is a stripped down version of the AMP meta-alb layer
#meant to be used independently outside the AMP solution.
BBPATH =. "${LAYERDIR}:"

BBFILE_COLLECTIONS += "desktop-distro"
BBFILE_PATTERN_desktop-distro := "^${LAYERDIR}"
BBFILE_PRIORITY_desktop-distro = "8"

BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % layer \
		for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % layer \
		for layer in BBFILE_COLLECTIONS.split())}"

BBFILES += " \
        ${LAYERDIR}/images/*.bb* \
        ${LAYERDIR}/packagegroups/*.bb* \
        ${LAYERDIR}/recipes-*/*/*.bb \
        ${LAYERDIR}/recipes-*/*/*.bbappend \
        ${LAYERDIR}/classes/*.bb* \
"

BBMASK += "meta-freescale/recipes-graphics/gtk\+/gtk.*\.bbappend"

LAYERSERIES_COMPAT_desktop-distro = "gatesgarth"

# Add EULA v22 version
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V22 = "3c3fe2b904fd694f28d2f646ee16dddb"

FSL_EULA_FILE_MD5SUMS_append = " \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V22} \
"

# Override the EULA defined in meta-freescale.
FSL_EULA_FILE = "${LAYERDIR}/EULA.txt"
FSL_EULA_FILE_MD5SUM = "${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V22}"

MACHINE_FEATURES_remove = "optee jailhouse"

FSL_GPU_MIRROR ?= "${FSL_MIRROR}"
